buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.neenbedankt.gradle.plugins:android-apt:1.8'
    }
}
repositories {
    mavenCentral()
    jcenter()
    maven { url "https://www.jitpack.io" }
    maven { url "http://dl.bintray.com/piasy/maven" }
}
apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'

def dbflow_version = "4.0.0-beta2"
def ciName = isEmpty(System.getenv("TRAVIS_TAG")) ? "Staging" : System.getenv("TRAVIS_TAG")
def ciCode = isEmpty(System.getenv("TRAVIS_BUILD_NUMBER")) ? 1000 : Integer.valueOf(System.getenv("TRAVIS_BUILD_NUMBER"))
def buglyPrdKey = isEmpty(System.getenv('BUGLY_PRD')) ? "" : System.getenv('BUGLY_PRD')
def isEmpty(String str) {
    return str == null || "".equals(str);
}

android {
    signingConfigs {
        config {
            keyAlias System.getenv('KEY_ALIAS')
            keyPassword System.getenv('KEY_PWD')
            storeFile file(projectDir.parentFile.absolutePath + '/houxg-leamonax.jks')
            storePassword System.getenv('KEYSTORE_PWD')
        }
    }
    compileSdkVersion 26
    buildToolsVersion "28.0.3"
    defaultConfig {
        applicationId "org.houxg.leamonax"
        minSdkVersion 21
        targetSdkVersion 26
        versionCode ciCode
        versionName ciName
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    lintOptions {
        abortOnError false
    }

    packagingOptions {
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.config
            buildConfigField "String", "BUGLY_KEY", String.format("\"%s\"", buglyPrdKey)
        }
    }

    buildTypes.each { buildType ->
        def properties = new Properties();
        if (buildType.isDebuggable()) {
            properties.load(new FileInputStream(new File(projectDir.absolutePath + "/staging.properties")))
            buildType.buildConfigField "String", "BUGLY_KEY", properties['BUGLY_KEY']
        }
    }
}


dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    androidTestImplementation('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    testImplementation 'junit:junit:4.12'
    implementation 'com.android.support:appcompat-v7:26.1.0'
    implementation 'com.android.support:support-v4:26.1.0'
    implementation 'com.android.support:design:26.1.0'
    implementation 'com.android.support:recyclerview-v7:26.1.0'
    implementation 'com.android.support:cardview-v7:26.1.0'

    implementation 'com.jakewharton:butterknife:8.4.0'
    annotationProcessor 'com.jakewharton:butterknife-compiler:8.4.0'

    implementation group: 'com.squareup.okhttp3', name: 'logging-interceptor', version: '3.4.1'
    implementation group: 'com.squareup.retrofit2', name: 'adapter-rxjava', version: '2.1.0'

    annotationProcessor "com.github.Raizlabs.DBFlow:dbflow-processor:${dbflow_version}"
    implementation "com.github.Raizlabs.DBFlow:dbflow-core:${dbflow_version}"
    implementation "com.github.Raizlabs.DBFlow:dbflow:${dbflow_version}"

    implementation 'org.greenrobot:eventbus:3.0.0'

    implementation 'io.reactivex:rxandroid:1.2.1'
    implementation 'io.reactivex:rxjava:1.1.9'
    implementation group: 'com.google.code.gson', name: 'gson', version: '2.7'
    implementation 'org.mongodb:mongo-java-driver:2.12.4'

    implementation 'com.github.bumptech.glide:glide:3.7.0'
    implementation 'jp.wasabeef:glide-transformations:2.0.1'
    implementation 'com.android.support:support-v13:24.1.0'
    implementation 'com.yuyh.imgsel:library:1.3.2'
    implementation 'com.facebook.stetho:stetho:1.4.1'
    implementation 'com.github.houxg:FlexLayout:1.2'

    implementation 'net.danlew:android.joda:2.9.5'
    implementation group: 'com.tencent.bugly', name: 'crashreport_upgrade', version: '1.2.1'
    implementation 'com.elvishew:xlog:1.3.0'
    implementation 'com.github.piasy:BigImageViewer:1.2.5'
    implementation 'com.github.piasy:GlideImageLoader:1.2.5'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
}
